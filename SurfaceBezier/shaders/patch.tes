#version 410 core

layout (quads, equal_spacing, cw) in;
uniform mat4 projview_matrix;
uniform mat4 model_matrix;
out vec4 positionOut;

const float epsilon = 0.001;
out vec3 normal;
out vec2 texCoords;

vec4 quadratic_bezier(vec4 A, vec4 B, vec4 C, float t)
{
    vec4 D = mix(A, B, t);
    vec4 E = mix(B, C, t);

    return mix(D, E, t);
}

vec4 cubic_bezier(vec4 A, vec4 B, vec4 C, vec4 D, float t)
{
    vec4 E = mix(A, B, t);
    vec4 F = mix(B, C, t);
    vec4 G = mix(C, D, t);

    return quadratic_bezier(E, F, G, t);
}

vec4 evaluate_patch(vec2 at)
{
    vec4 P[4];
    int i;

    for (i = 0; i < 4; i++)
    {
        P[i] = cubic_bezier(gl_in[i + 0].gl_Position, gl_in[i + 4].gl_Position, gl_in[i + 8].gl_Position, gl_in[i + 12].gl_Position, at.y);
    }

    return cubic_bezier(P[0], P[1], P[2], P[3], at.x);
}

void main(void)
{
    vec4 p1 = evaluate_patch(gl_TessCoord.xy);
	texCoords = gl_TessCoord.xy;

	//calculate normal
    vec4 p2 = evaluate_patch(gl_TessCoord.xy + vec2(0.0, epsilon));
    vec4 p3 = evaluate_patch(gl_TessCoord.xy + vec2(epsilon, 0.0));

    vec3 v1 = normalize(p2.xyz - p1.xyz);
    vec3 v2 = normalize(p3.xyz - p1.xyz);

    normal = cross(v1, v2);

	//output
	positionOut = p1;
    gl_Position = projview_matrix * p1;
}